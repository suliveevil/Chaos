---
title: "文件删除"
categories: 
date: 2022-10-14 11:30:55 +0800
tags: 
excerpt: 
---




### 删除特定文件扩展名的相同文件名的文件

[macos - Mac script for deleting a file if another file with same name and different extension is not present in the folder - Stack Overflow](https://stackoverflow.com/questions/19468657/mac-script-for-deleting-a-file-if-another-file-with-same-name-and-different-exte)

```text
1.jpg
1.nef
2.nef
3.jpg
3.nef
```


```shell
#! /usr/bin/bash

# delete the .nef files if a .jpg file with same name is not present
cd /path/to/directory; for f in *.nef; do [[ -e ${f%nef}jpg ]] || echo rm "$f"; done # Remove `echo` to actually delete the files.

# delete nef file only if the jpg existed
cd /path/to/directory; for f in *.nef; do [[ -e ${f%nef}jpg ]] && echo rm "$f"; done # Remove `echo` to actually delete the files.
```


```applescript
-- 1
set ff to choose folder

tell application "System Events"
    set everyNef to files of ff whose name extension = "nef"
    repeat with thisNef in my everyNef
        set thisJpg to my getBaseName(thisNef) & ".jpg"
        if not (exists file thisJpg of ff) then delete thisNef
    end repeat
end tell

-- This handler is not limited to .jpg
on getBaseName(aFile)
    tell application "System Events" to set {fileName, fileExt} to {name, name extension} of aFile
    return text 1 thru ((get offset of "." & fileExt in fileName) - 1) of fileName
end getBaseName

-- 2

set ff to choose folder
set ffText to ff as text

script s
    property everyFileName : missing value
end script

set inTime to current date
tell application "System Events"
    set s's everyFileName to name of files of ff
end tell

repeat with aName in s's everyFileName
    if (text -3 thru -1 of aName) is "nef" then
        if (text 1 thru -4 of aName & "jpg") is not in s's everyFileName then
            tell application "System Events" to delete file (ffText & aName)
        end if
    end if
end repeat
set s's everyFileName to missing value

set totalTime to (current date) - inTime
return totalTime

-- 3

set macStyleFF to choose folder
set ff to (POSIX path of macStyleFF)
tell application "Finder"
    --first get all the .nef files (names)
    set everyNef to do shell script "cd '" & ff & "';ls *.nef" --name of every file of ff whose name ends with ".nef"
    set everyNef to (every paragraph of everyNef)

    repeat with thisNef in everyNef
        --use this list to see if there is a jpg of same name
        if file ((text 1 thru -5 of thisNef) & ".jpg") of macStyleFF exists then
            --it has a companion
        else
            --if not, send it to the trash
            delete (file thisNef of macStyleFF)
        end if
    end repeat
end tell


```








